import os
import urllib2 as urllib
import yaml
import subprocess
import shutil
import carousel

pandoc_slides=Builder(action='pandoc -t revealjs -s -V theme=night'+
    ' --css=night.css'+
    ' --css=slidetheme.css'+
    ' --mathjax '+
    ' -V revealjs-url=http://lab.hakim.se/reveal-js/'+
    ' $SOURCES -o $TARGET')

def assetpath(node):
    return os.path.join('reveal','assets',
        os.path.basename(os.path.splitext(env.GetBuildPath(node))[0])+'.png')
# The next step uses the module from https://github.com/jamespjh/jamespjh-wsd/
# Install via python setup.py, then put a ~/.wsd/config.yml with 
# key: YOURWSDKEY
python_figure_builder=Builder(action='python $SOURCE $TARGET',
    suffix='.png',
    src_suffix='.py')
    
dot_figure_builder=Builder(action='dot -Tpng $SOURCE -o $TARGET',
    suffix='.png',
    src_suffix='.dot')
    
shell_figure_builder=Builder(action='bash $SOURCE $TARGET',
    suffix='.png',
    src_suffix='.sh') 
    
carousel_builder=Builder(action=carousel.carousel_action, 
    suffix=".png",src_suffix=".carousel")   
                             
def wget_each_url(target,source,env):
    data=yaml.load(open(source[0].path))
    for target,source in zip(target,data.values()):
        content=urllib.urlopen(source)
        result=open(target.path,'w')
        result.write(content.read())
        result.close()

def yaml_emitter(target,source,env):
    data=yaml.load(open(source[0].path))
    targets=[os.path.join('reveal','assets',fname) for fname in data.keys()]
    return targets, source 
    
def browse_each_url(target,source,env):
    data=yaml.load(open(source[0].path))
    for target,source in zip(target,data.values()):
        subprocess.call(["webkit2png","--delay=1","-s1","-F","-otemp", source])
        shutil.move("temp-full.png",target.path)

wget_builder=Builder(action=wget_each_url,
    emitter=yaml_emitter)
    
browse_builder=Builder(action=browse_each_url,
        emitter=yaml_emitter)
        
env=Environment()
env.Append(BUILDERS={'PY':python_figure_builder})
env.Append(BUILDERS={'Dot':dot_figure_builder})
env.Append(BUILDERS={'Shell':shell_figure_builder})
env.Append(BUILDERS={'Wget':wget_builder})
env.Append(BUILDERS={'Browse':browse_builder})
env.Append(BUILDERS={'Carousel':carousel_builder}) 
 
env.Append(BUILDERS={'PandocSlides':pandoc_slides})
lecture=env.PandocSlides('reveal/index.html','lecture.md')

for source in Glob('assets/*.py'):
    png=env.PY(assetpath(source),source)
    Depends(lecture,png)
for source in Glob('assets/*.dot'):
    png=env.Dot(assetpath(source),source)
    Depends(lecture,png)
for source in Glob('assets/*.sh'):
    png=env.Shell(assetpath(source),source)
    Depends(lecture,png)
for source in Glob('assets/*.wget'):
    results=env.Wget(assetpath(source),source)
    Depends(lecture,results)
for source in Glob('assets/*.browse'):
    results=env.Browse(assetpath(source),source)
    Depends(lecture,results)  
for source in Glob('assets/*.carousel'):
    results=env.Carousel(assetpath(source),source)
    Depends(lecture,results)
       